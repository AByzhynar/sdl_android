apply plugin: 'java-library'

group 'com.smartdevicelink'
version '4.7.2'

sourceCompatibility = 1.7

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

repositories {
    google()
    jcenter()
}

// This extraLibs solution is explained here: https://discuss.gradle.org/t/how-to-include-dependencies-in-jar/19571/5
configurations {
    // configuration that holds jars to include in the jar
    extraLibs
}

dependencies {
    extraLibs fileTree(dir: 'libs', include: ['*.jar'])
    extraLibs project(path: ':javaSE')
    configurations.api.extendsFrom(configurations.extraLibs)
}

jar {
    dependsOn ':javaSE:jar'
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

apply from: 'bintray.gradle'

// Exclude :javaSE:bintrayUpload from running when we run bintrayUpload in javaEE
def taskRequests = gradle.startParameter.taskRequests
def runTaskRequest = taskRequests.find { it.args.contains('bintrayUpload') }
if (runTaskRequest) {
    gradle.startParameter.excludedTaskNames = [':javaSE:bintrayUpload']
}